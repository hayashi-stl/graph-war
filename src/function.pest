func = { SOI ~ expr ~ EOI }
expr = { add }
add = { mul ~ (add_sign ~ mul)* }
mul = { neg ~ (mul_sign ~ neg)* }
neg = { "-"? ~ exp }
exp = { primary ~ ("^" ~ primary )* }
primary = { call_2 | call_1 | primitive }
primitive = { var | constant | "(" ~ expr ~ ")" }
call_1 = { name ~ primitive }
call_2 = { name ~ primitive ~ primitive }
var = { name }
constant = @{ "-"? ~ ASCII_DIGIT+ ~ ("." ~ ASCII_DIGIT+)? }

assigns = { SOI ~ ( assign ~ (assign_sep ~ assign)* | "" ) ~ assign_sep? ~ EOI }
assign_sep = _{ NEWLINE+ }
assign = { var ~ "=" ~ expr }

name = @{ ASCII_ALPHA ~ ASCII_ALPHANUMERIC* }
mul_sign = { "*" | "//" | "/" | "%" }
add_sign = { "+" | "-" }

WHITESPACE = _{ " " }